package parsers.statpy;
import java_cup.runtime.*;
import java.util.LinkedList;
import java.util.HashMap;
import java.util.stream.Collectors;

parser code
{:
    public void syntax_error(Symbol s){
        main.ParserState.output.add("Error Sintáctico en la Línea " + (s.left) +
                " Columna "+s.right+ ". No se esperaba este componente: " +s.value+"."); 
    }

    public void unrecovered_syntax_error(Symbol s){ 
        main.ParserState.output.add("Error síntactico irrecuperable en la Línea " + 
                (s.left)+ " Columna "+s.right+". Componente " + s.value + 
                " no reconocido."); 
    }

    String title;
    String titleX;
    String titleY;
    Object[] valueArray;
    Object[] titleArray;
    HashMap<String, String> symbol_table = new HashMap<>();
:}

terminal String LPAREN, RPAREN, ADD, SUB, DIV, TIMES, SEMICOLON, COMMA;
terminal String STRING_LITERAL, INTEGER_LITERAL, DOUBLE_LITERAL, CHAR_LITERAL;
terminal String STRING, INTEGER, DOUBLE, CHAR, BOOL;
terminal String TRUE, FALSE, AND, OR, NOT, ASSIGN, ID;
terminal String GREATER, LESS, GREATER_EQUAL, LESS_EQUAL, EQUAL, NOT_EQUAL;
terminal String MINUS, PRINT, VOID, MAIN, LBRACKET, RBRACKET;
terminal String FOR, INC, WHILE, DO, SWITCH, CASE, DEFAULT, COLON, BREAK;
terminal String IF, ELSE;
terminal String SQR_BRACKETS, EJEX, VALORES, TITULO, TITULOX, TITULOY;
terminal String DEFINIR_GLOBALES, GRAFICA_BARRAS, GRAFICA_PIE;
terminal String DOLLAR, NEW_VALOR;

non terminal LinkedList document;
non terminal String expr, binary_expr, unary_expr, type;
non terminal String expr_literal, arithmetic_expr, relational_expr;
non terminal LinkedList<String> stmts, stmt;
non terminal LinkedList<String> variable_stmt, var_assignment, var_declaration;
non terminal LinkedList<String> print_stmt;
non terminal LinkedList<String> for_stmt, while_stmt, do_while_stmt, switch_stmt;
non terminal LinkedList<String> cases, case, default_case;
non terminal LinkedList<String> if_stmt, else_stmt, elif_stmt, continue_if;
non terminal LinkedList<String> list_items;
non terminal define_global_vars, bar_graph, pie_graph;
non terminal bar_graph_body, pie_graph_body;
non terminal bar_graph_options, pie_graph_options;
non terminal global_vars;
non terminal String json_value;
non terminal String translation_exp;
non terminal String logical_op;

precedence left GREATER, GREATER_EQUAL, LESS, LESS_EQUAL, EQUAL, NOT_EQUAL;
precedence left AND, OR;
precedence left NOT;
precedence left ADD, SUB;
precedence left TIMES, DIV;
precedence left MINUS;

document
::= VOID MAIN LPAREN RPAREN LBRACKET stmts:list1 RBRACKET
    {:
        LinkedList<String> translation = new LinkedList<>();
        translation.add("def main():");
        translation.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
        translation.add("if __name__ == '__main__':");
        translation.add("\tmain()");
        translation.forEach(stmt -> main.ParserState.output.add(stmt));
        main.ParserState.output.add("--Parseo finalizado--");
    :};

stmts
::= stmts:list stmt:stmt
    {:
        if(stmt != null) list.addAll(stmt);
        RESULT = list;
    :}
    |
    stmt:stmt
    {:
        RESULT = new LinkedList<String>();
        if(stmt != null) RESULT.addAll(stmt);
    :};

stmt
::= variable_stmt:stmt SEMICOLON
    {: RESULT = stmt; :}
    |
    print_stmt:stmt SEMICOLON
    {: RESULT = stmt; :}
    |
    if_stmt:stmt
    {: RESULT = stmt; :}
    |
    for_stmt:stmt
    {: RESULT = stmt; :}
    |
    while_stmt:stmt
    {: RESULT = stmt; :}
    |
    do_while_stmt:stmt SEMICOLON
    {: RESULT = stmt; :}
    |
    switch_stmt:stmt
    {: RESULT = stmt; :}
    |
    BREAK:v SEMICOLON
    {:
        RESULT = new LinkedList<String>();
        RESULT.add(v);
    :}
    |
    define_global_vars
    |
    bar_graph
    |
    pie_graph;

//---INICIO SECCION DE GRAFICAS---
translation_exp
::= expr_literal:v
    {: RESULT = v; :}
    |
    ID:key
    {:
        try{
            RESULT = symbol_table.get(key.toLowerCase());
        }
        catch(Exception e){
            main.ParserState.output.add("Error en ("+keyleft+", "+keyright+"): "+key.toLowerCase()+" no existe");
        }
    :}
    |
    json_value:v
    {: RESULT = v; :};

json_value
::= DOLLAR LBRACKET NEW_VALOR COMMA STRING_LITERAL:file COMMA STRING_LITERAL:key RBRACKET
    {:
        try{
            RESULT = (String) main.ParserState.json_files.get(file.toLowerCase()).get(key.toLowerCase());
        }
        catch(Exception e){
            main.ParserState.output.add("Error en ("+keyleft+", "+keyright+"): "+key.toLowerCase()+" no existe");
        }
    :};

define_global_vars
::= VOID DEFINIR_GLOBALES LPAREN RPAREN LBRACKET global_vars RBRACKET;

global_vars
::= global_vars type ID:k ASSIGN translation_exp:v SEMICOLON
    {: symbol_table.put(k.toLowerCase(), v); :}
    |
    type ID:k ASSIGN translation_exp:v SEMICOLON
    {: symbol_table.put(k.toLowerCase(), v); :};

bar_graph
::= VOID GRAFICA_BARRAS LPAREN RPAREN LBRACKET bar_graph_body RBRACKET
    {: graph.general.barras(title, titleX, titleY, valueArray, titleArray); :};

bar_graph_body
::= bar_graph_body bar_graph_options
    |
    bar_graph_options;

bar_graph_options
::= STRING TITULO ASSIGN translation_exp:v SEMICOLON
    {: title = v; :}
    |
    STRING TITULOX ASSIGN translation_exp:v SEMICOLON
    {: titleX = v; :}
    |
    STRING TITULOY ASSIGN translation_exp:v SEMICOLON
    {: titleY = v; :}
    |
    STRING SQR_BRACKETS EJEX ASSIGN LBRACKET list_items:arr RBRACKET SEMICOLON
    {:
        titleArray = arr.toArray();
    :}
    |
    DOUBLE SQR_BRACKETS VALORES ASSIGN LBRACKET list_items:arr RBRACKET SEMICOLON
    {:
        valueArray = arr.toArray();
    :};

pie_graph
::= VOID GRAFICA_PIE LPAREN RPAREN LBRACKET pie_graph_body RBRACKET
    {: graph.general.pie(title, valueArray, titleArray); :};

pie_graph_body
::= pie_graph_body pie_graph_options
    |
    pie_graph_options;

pie_graph_options
::= STRING TITULO ASSIGN translation_exp:v SEMICOLON
    {: title = v; :}
    |
    STRING SQR_BRACKETS EJEX ASSIGN LBRACKET list_items:arr RBRACKET SEMICOLON
    {:
        titleArray = arr.toArray();
    :}
    |
    DOUBLE SQR_BRACKETS VALORES ASSIGN LBRACKET list_items:arr RBRACKET SEMICOLON
    {:
        valueArray = arr.toArray();
    :};

list_items
::= list_items:list COMMA translation_exp:item
    {:
        list.add(item);
        RESULT = list;
    :}
    |
    translation_exp:item
    {:
        LinkedList list = new LinkedList();
        list.add(item);
        RESULT = list;
    :};
//---FIN SECCION DE GRAFICAS---

variable_stmt
::= var_declaration:v
    {: RESULT = v; :}
    |
    var_assignment:v
    {: RESULT = v; :};

var_declaration
::= type ID:k ASSIGN expr:v
    {:
        RESULT = new LinkedList<String>();
        RESULT.add(k + " = " + v);
    :}
    |
    type ID:k
    {:
        RESULT = new LinkedList<String>();
        RESULT.add(k + " = None");
    :};

var_assignment
::= ID:k ASSIGN expr:v
    {:
        RESULT = new LinkedList<String>();
        RESULT.add(k + " = " + v);
    :};

print_stmt
::= PRINT LPAREN expr:e RPAREN
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("print("+e+")");
    :};

if_stmt
::= IF LPAREN expr:e RPAREN LBRACKET stmts:list1 RBRACKET continue_if:list2 else_stmt:list3
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("if "+e+" :");
        RESULT.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
        RESULT.addAll(list2.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
        RESULT.addAll(list3.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
    :}
    |
    IF LPAREN expr:e RPAREN LBRACKET stmts:list1 RBRACKET continue_if:list2
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("if "+e+" :");
        RESULT.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
        RESULT.addAll(list2.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
    :}
    |
    IF LPAREN expr:e RPAREN LBRACKET stmts:list1 RBRACKET else_stmt:list2
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("if "+e+" :");
        RESULT.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
        RESULT.addAll(list2.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
    :}
    |
    IF LPAREN expr:e RPAREN LBRACKET stmts:list1 RBRACKET
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("if "+e+" :");
        RESULT.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
    :};

continue_if
::= continue_if:list elif_stmt:stmt
    {:
        list.addAll(stmt);
        RESULT = list;
    :}
    |
    elif_stmt:stmt
    {:
        RESULT = new LinkedList<String>();
        RESULT.addAll(stmt);
    :};

elif_stmt
::= ELSE IF LPAREN expr:e RPAREN LBRACKET stmts:list1 RBRACKET
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("elif "+e+" :");
        RESULT.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
    :};

else_stmt
::= ELSE LBRACKET stmts:list1 RBRACKET
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("else :");
        RESULT.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
    :};

for_stmt
::= FOR LPAREN type ID:var ASSIGN expr_literal:from SEMICOLON ID logical_op expr_literal:to SEMICOLON ID INC RPAREN
    LBRACKET stmts:list1 RBRACKET
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("for "+var+" in range("+from+","+to+"):");
        RESULT.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
    :};

logical_op
::= LESS
    |
    LESS_EQUAL
    |
    GREATER_EQUAL
    |
    GREATER
    |
    EQUAL
    |
    NOT_EQUAL;

while_stmt
::= WHILE LPAREN expr:e RPAREN LBRACKET stmts:list1 RBRACKET
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("while "+e+":");
        RESULT.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
    :};

do_while_stmt
::= DO LBRACKET stmts:list1 RBRACKET WHILE LPAREN expr:e RPAREN
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("while True:");
        RESULT.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
        RESULT.add("\tif "+e+":");
        RESULT.add("\t\tbreak");
    :};

switch_stmt
::= SWITCH LPAREN expr:e RPAREN LBRACKET cases:list1 default_case:list2 RBRACKET
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("def switch("+e+"):");
        RESULT.add("\tswitcher = {");
        RESULT.addAll(list1.stream().map(item -> "\t\t" + item).collect(Collectors.toCollection(LinkedList::new)));
        RESULT.addAll(list2.stream().map(item -> "\t\t" + item).collect(Collectors.toCollection(LinkedList::new)));
        RESULT.add("\t}");
    :};

cases
::= cases:list case:stmt
    {:
        list.addAll(stmt);
        RESULT = list;
    :}
    |
    case:stmt
    {:
        RESULT = new LinkedList<String>();
        RESULT.addAll(stmt);
    :};

case
::= CASE expr_literal:e COLON stmts:list1
    {:
        RESULT = new LinkedList<String>();
        RESULT.add(e+":");
        RESULT.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
    :};

default_case
::= DEFAULT COLON stmts:list1
    {:
        RESULT = new LinkedList<String>();
        RESULT.add("0:");
        RESULT.addAll(list1.stream().map(item -> "\t" + item).collect(Collectors.toCollection(LinkedList::new)));
    :};

type
::= INTEGER:v
    {: RESULT = v; :}
    |
    DOUBLE:v
    {: RESULT = v; :}
    |
    STRING:v
    {: RESULT = v; :}
    |
    CHAR:v
    {: RESULT = v; :}
    |
    VOID:v
    {: RESULT = v; :}
    |
    BOOL:v
    {: RESULT = v; :};

expr
::= binary_expr:v
    {: RESULT = v; :}
    |
    unary_expr:v
    {: RESULT = v; :}
    |
    LPAREN expr:v RPAREN
    {: RESULT = v; :}
    |
    expr_literal:v
    {: RESULT = v; :}
    |
    ID:v
    {: RESULT = v; :};

binary_expr
::= arithmetic_expr:v
    {: RESULT = v; :}
    |
    relational_expr:v
    {: RESULT = v; :}
    |
    expr:e1 AND expr:e2
    {: RESULT = "" + e1 + " and " + e2; :}
    |
    expr:e1 OR expr:e2
    {: RESULT = "" + e1 + " or " + e2; :};

unary_expr
::= NOT expr:e
    {: RESULT = "not " + e; :}
    |
    SUB expr:e 
    {: RESULT = "-" + e; :}
    %prec MINUS;

arithmetic_expr
::= expr:e1 ADD:op expr:e2
    {: RESULT = "" + e1 + op + e2; :}
    |
    expr:e1 SUB:op expr:e2
    {: RESULT = "" + e1 + op + e2; :}
    |
    expr:e1 TIMES:op expr:e2
    {: RESULT = "" + e1 + op + e2; :}
    |
    expr:e1 DIV:op expr:e2
    {: RESULT = "" + e1 + op + e2; :};

relational_expr
::= expr:e1 GREATER:op expr:e2
    {: RESULT = "" + e1 + op + e2; :}
    |
    expr:e1 LESS:op expr:e2
    {: RESULT = "" + e1 + op + e2; :}
    |
    expr:e1 GREATER_EQUAL:op expr:e2
    {: RESULT = "" + e1 + op + e2; :}
    |
    expr:e1 LESS_EQUAL:op expr:e2
    {: RESULT = "" + e1 + op + e2; :}
    |
    expr:e1 EQUAL:op expr:e2
    {: RESULT = "" + e1 + op + e2; :}
    |
    expr:e1 NOT_EQUAL:op expr:e2
    {: RESULT = "" + e1 + op + e2; :};

expr_literal
::= STRING_LITERAL:v
    {: RESULT = v; :}
    |
    INTEGER_LITERAL:v
    {: RESULT = v; :}
    |
    CHAR_LITERAL:v
    {: RESULT = v; :}
    |
    DOUBLE_LITERAL:v
    {: RESULT = v; :}
    |
    TRUE:v
    {: RESULT = v; :}
    |
    FALSE:v
    {: RESULT = v; :};
